/*
     Handling an object with dynamic keys for sub-objects.
     example:
     {
         static: "hello",
         dynamics: {
             "undetermined1": 2
             "undetermined2": 6
         }
     }

     Where the "undetermined" keys, are unknown at compile time.
     Could be dynamic JS keys generated by user or generally at run time.
 */

type obj = {
  static: string,
  dynamics: Js.Dict.t(int),
};

module Of_json = {
  let obj = json =>
    Melange_json.Of_json.{
      static: json |> field("static", string),
      dynamics: json |> field("dynamics", js_dict(int)),
    };
};

module To_json = {
  let obj = c => {
    Melange_json.To_json.(
      json_dict(
        Js.Dict.fromList([
          ("static", string(c.static)),
          ("dynamics", c.dynamics |> js_dict(int)),
        ]),
      )
    );
  };
};

let data = {| {
  "static": "hi",
  "dynamics": { "hello": 5, "random": 8 }
} |};

let decodedData = data |> Melange_json.of_string |> Of_json.obj;

/*
 Will log [ 'hi', { hello: 5, random: 8 } ]
 */
let _ = decodedData |> Js.log;

/*
 Will log { static: 'hi', dynamics: { hello: 5, random: 8 } }
 */
let encodedDataBack = decodedData |> To_json.obj |> Js.log;
